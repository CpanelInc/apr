#!/usr/local/cpanel/3rdparty/bin/perl
# cpanel - find-latest-version                  Copyright(c) 2019 cPanel, L.L.C.
#                                                           All rights Reserved.
# copyright@cpanel.net                                         http://cpanel.net
# This code is subject to the cPanel license. Unauthorized copying is prohibited
#
# This provides incremental updates to existing packages in EasyApache4.
package apr::find_latest_version;

use strict;
use warnings;

use lib "../ea-tools/lib/ea4_tool";    # assumes ea-tools is checked out next to this repo
use ea4_tool::util ();

unless ( caller() ) {
    ea4_tool::util::find_latest_version( \&_get_required, \&_add_sum );
}

###############
#### helpers ##
###############

sub _get_required {
    my ($http) = @_;

    my $res = $http->get("http://www.apache.org/dist/apr/");
    if ( !$res->{success} ) {
        die "Could not GET apr dist page ($res->{status} $res->{reason})\n";
    }

    my @apr = sort { $b <=> $a } ( $res->{content} =~ m{href="apr-1\.([0-9]+\.[0-9]+)\.tar\.gz"}g );
    if ( !@apr ) {
        die "Could not find apr versions\n";
    }

    my $version = "1\.$apr[0]";
    my $name    = "apr-$version.tar.gz";
    my $url     = "http://www.apache.org/dist/apr/$name";

    return ( $version, $url, $name );
}

sub _add_sum {
    my ( $http, $hr ) = @_;

    my $checksum_url = "http://www.apache.org/dist/apr/apr-$hr->{version}.tar.gz.sha256";
    my $res          = $http->get($checksum_url);
    if ( !$res->{success} ) {
        die "Could not GET apr SHASUMS page ($res->{status} $res->{reason})\n";
    }

    my $name     = $hr->{tarball}{name};
    my $checksum = ( $res->{content} =~ /^((?i)[0-9a-f]+)\s+\Q*$name\E$/m )[0];
    unless ($checksum) {
        die "No checksum for $name at $checksum_url:\nContent: $res->{content}\n";
    }

    $hr->{tarball}{sum}{hex}  = $checksum;
    $hr->{tarball}{sum}{type} = "sha256";

    return;
}
